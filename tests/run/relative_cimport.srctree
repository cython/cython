# mode: run
# tag: cimport

PYTHON setup.py build_ext --inplace
PYTHON -c "from pkg.a import test; assert test() == (5, 7)"
PYTHON -c "from pkg.b import test; assert test() == (1, 2)"
PYTHON -c "from pkg.b_py2 import test; assert test() == (1, 2)"
PYTHON -c "from pkg.sub.c import test; assert test() == (1, 2)"
PYTHON -c "from pkg.sub.d import test; assert test() == 3"

######## setup.py ########

from distutils.core import setup
from Cython.Build import cythonize
from Cython.Distutils.extension import Extension

setup(
    ext_modules=cythonize('**/*.pyx'),
)


######## pkg/__init__.py ########

######## pkg/sub/__init__.py ########

######## pkg/a.pyx ########

from .sub.reimport cimport myint
from .sub cimport c

cdef myint i = 5
assert i == 5
assert c.sum_func(1, 2) == 3

from .sub cimport myint as pkg_myint

cdef pkg_myint pi = 7
assert pi == 7

cdef class test_pxd:
    pass


def test():
    return (i, pi)


######## pkg/a.pxd ########

cdef class test_pxd:
    cdef public int x
    cdef public int y


######## pkg/b.pyx ########

from . cimport a
from .a cimport test_pxd

assert a.test_pxd is test_pxd

def test():
    cdef test_pxd obj = test_pxd()
    obj.x = 1
    obj.y = 2
    return (obj.x, obj.y)


######## pkg/b_py2.pyx ########

# cython: language_level=2

from . cimport a
from .a cimport test_pxd
cimport a as implicitly_relative_a  # <-- Py2 "feature"

assert a.test_pxd is test_pxd
assert implicitly_relative_a.test_pxd is test_pxd

def test():
    cdef test_pxd obj = test_pxd()
    obj.x = 1
    obj.y = 2
    return (obj.x, obj.y)


######## pkg/sub/c.pyx ########

from ..a cimport test_pxd


def test():
    cdef test_pxd obj = test_pxd()
    obj.x = 1
    obj.y = 2
    return (obj.x, obj.y)

cdef int sum_func(int n, int m):
    return n + m

######## pkg/sub/c.pxd ########

cdef int sum_func(int n, int m)

######## pkg/sub/d.pyx ########

from . cimport c

def test():
    return c.sum_func(1, 2)

######## pkg/sub/tdef.pxd ########

ctypedef int myint


######## pkg/sub/reimport.pxd ########

from .tdef cimport myint


######## pkg/sub/__init__.pxd ########

from .tdef cimport myint
from . cimport c
